# see https://www.w3.org/TR/turtle/#sec-grammar

# non-terminals

<start>                 ::= <turtleDoc> "\n\n"
<turtleDoc>             ::= <statement> | <statement> "\n" <turtleDoc>
<statement>             ::= <directive> | <triples> " ."
<directive>             ::= <prefixID> | <base> | <sparqlPrefix> | <sparqlBase>
<prefixID>              ::= "@prefix " <PNAME_NS> " " <IRIREF> " ."
<base>                  ::= "@base " <IRIREF> " ."
<sparqlBase>            ::= "BASE " <IRIREF> " ."
<sparqlPrefix>          ::= "PREFIX " <PNAME_NS> " " <IRIREF> " ."
<triples>               ::= <subject> " " <predicateObjectList> | <blankNodePropertyList> " " <predicateObjectList>
<predicateObjectList>   ::= <verb> " " <objectList> | <verb> " " <objectList> " " <predicateObjectListInner>
<predicateObjectListInner>  ::= ";\n" | ";\n" <verb> " " <objectList> | <predicateObjectListInner> " " <predicateObjectListInner>
<objectList>            ::= <object> | <object> " , " <objectList>
<verb>                  ::= <predicate> | " a "
<subject>               ::= <iri> | <blankNode> | <collection>
<predicate>             ::= <iri>
<object>                ::= <iri> | <blankNode> | <collection> | <blankNodePropertyList> | <literal>
<literal>               ::= <rdfLiteral> | <numericLiteral> | <booleanLiteral>
<blankNodePropertyList> ::= "[" <predicateObjectList> "]"
<collection>            ::= "()" | "(" <collectionInner> ")"
<collectionInner>       ::= <object> | <object> <collectionInner>
<numericLiteral>        ::= <INTEGER> | <DECIMAL> | <DOUBLE> 
<rdfLiteral>            ::= <string> | <string> <LANGTAG> | <string> "^^" <iri>
<booleanLiteral>        ::= "true" | "false"
<string>                ::= <STRING_LITERAL_QUOTE> | <STRING_LITERAL_SINGLE_QUOTE> | <STRING_LITERAL_LONG_SINGLE_QUOTE> | <STRING_LITERAL_LONG_QUOTE>
<iri>                   ::= <IRIREF> | <prefixedName> " "       
<prefixedName>          ::= <PNAME_LN> | <PNAME_NS>
<blankNode>             ::= <BLANK_NODE_LABEL> | <ANON>


# terminals

<IRIREF>            ::= "<i>" | "<j>" 
<PNAME_NS>          ::= "p1:" | "p2:"
<PNAME_LN>          ::= <PNAME_NS> "c" | <PNAME_NS> "d"
<BLANK_NODE_LABEL>  ::= ":" | "_:b" | "_:b2"
<LANGTAG>           ::= "@some-lang" | "@lang"
<INTEGER>           ::= "0" | "-12" | "+3"
<DECIMAL>           ::= "5" |"-3.23" | "+.7"
<DOUBLE>            ::= "+4.e5" | "1234E-3" | "-.2e3"
<STRING_LITERAL_QUOTE>              ::= "\"s\""
<STRING_LITERAL_SINGLE_QUOTE>       ::= "'t'"
<STRING_LITERAL_LONG_SINGLE_QUOTE>  ::= "'''u'''"
<STRING_LITERAL_LONG_QUOTE>         ::= "\"\"\"v\"\"\""
<ANON>              ::= "[\t]" | "[ ]"
