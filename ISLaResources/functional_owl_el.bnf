# see https://www.w3.org/TR/owl2-profiles/#OWL_2_EL_2

# TODO: consider "global restrictions" for EL profile from https://www.w3.org/TR/owl2-profiles/


# comment: additional <xyzList> non-terminals represent (possibly empty) lists of these nonterminals


################# non-terminals ####################################

<start>         ::= <ontologyDocument>

<ontologyDocument>	::= <Prefixes> <ontology>
<ontology>	::= "Ontology (\n\n" <ontologyAnnotations> "\n" <declarations> "\n" <axioms> " \n)\n"

<axioms>     ::= <axiom> "\n\n" <axiom> "\n\n" <axiom> "\n\n" <axiom> "\n\n" <axiom> | <axiom> "\n\n" <axiom> "\n\n" <axiom> "\n\n" <axioms>

# general part

<ontologyAnnotations>	::= <annotationList>

<axiomAnnotations>	::= <annotationList>

<annotation>	::= "Annotation(" <annotationAnnotations> " " <AnnotationProperty> " " <AnnotationValue> ")"
<annotationList>	::= "" | <annotation> " " <annotationList>
<annotationAnnotations>	::= <annotationList>

# EL specific

<individualList>	::= "" | <Individual> " " <individualList>

<literal>	::= <typedLiteral> | <stringLiteralNoLanguage> #| <stringLiteralWithLanguage>
<typedLiteral>	::= <lexicalForm> "^^" <Datatype> #| <lexicalForm> "^^" <NewDatatype> 
<lexicalForm>	::= <QuotedString>
<stringLiteralNoLanguage> 	::= <QuotedString>
#<stringLiteralWithLanguage>	::= <QuotedString> <LanguageTag>


<objectPropertyExpression>	::= <NonSimpleObjectProperty> | <SimpleObjectProperty>
<objectPropertyExpressionList>	::= "" | <objectPropertyExpression> " " <objectPropertyExpressionList>

# introduced to comform to https://www.w3.org/TR/2012/REC-owl2-syntax-20121211/#Property_Hierarchy_and_Simple_Object_Property_Expressions
<simpleObjectPropertyExpression>    ::= <SimpleObjectProperty>
<nonSimpleObjectPropertyExpression>    ::= <NonSimpleObjectProperty>

<dataPropertyExpression>	::= <DataProperty>
<dataPropertyExpressionList>	::= "" | <dataPropertyExpression> " " <dataPropertyExpressionList>

<dataRange>	::= <Datatype> | <dataIntersectionOf> | <dataOneOf>
<dataRangeList>	::= "" | <dataRange> " " <dataRangeList>

<dataIntersectionOf>	::= "DataIntersectionOf(" <dataRange>  " " <dataRange> " " <dataRangeList> ") "

<dataOneOf>	::= "DataOneOf(" <literal> ") "


<classExpression>	::= <Class> | <objectIntersectionOf> | <objectOneOf> | <objectSomeValuesFrom> | <objectHasValue> | <objectHasSelf> | <dataSomeValuesFrom> | <dataHasValue>

<classExpressionList>	::= "" | <classExpression> " " <classExpressionList>

<objectIntersectionOf>	::= "ObjectIntersectionOf(" <classExpression> " " <classExpression> " " <classExpressionList> ")"

<objectOneOf>	::= "ObjectOneOf(" <Individual> ")"

<objectSomeValuesFrom>	::= "ObjectSomeValuesFrom(" <objectPropertyExpression> " " <classExpression> ")"

<objectHasValue>	::= "ObjectHasValue(" <objectPropertyExpression> " " <Individual> ")"

<objectHasSelf>		::= "ObjectHasSelf(" <simpleObjectPropertyExpression> ")"

#<dataSomeValuesFrom>	::= "DataSomeValuesFrom(" <dataPropertyExpression> " " <dataPropertyExpressionList> " " <dataRange>")"
<dataSomeValuesFrom>	::= "DataSomeValuesFrom(" <dataPropertyExpression> " " <dataRange>")"	# we only have data ranges with arity 1

<dataHasValue>		::= "DataHasValue(" <dataPropertyExpression> " " <literal> ")"	



# main production rule
<axiom>		::= <classAxiom> | <objectPropertyAxiom> | <dataPropertyAxiom> | <hasKey> | <assertion> # | <dataTypeDefinition>  | <annotationAxiom>


<classAxiom>	::= <subClassOf> | <equivalentClasses> | <disjointClasses>

<subClassOf>	::= "SubClassOf(" <axiomAnnotations> " " <subClassExpression> " " <superClassExpression> ")"
<subClassExpression>	::= <classExpression>
<superClassExpression>	::= <classExpression>

<equivalentClasses>	::= "EquivalentClasses(" <axiomAnnotations> " " <classExpression> " " <classExpression> " " <classExpressionList> ")"

<disjointClasses>	::= "DisjointClasses(" <axiomAnnotations> " " <classExpression> " " <classExpression> " " <classExpressionList> ")"


<objectPropertyAxiom>	::= <equivalentObjectProperties> | <subObjectPropertyOf> | <objectPropertyDomain> | <objectPropertyRange> | <reflexiveObjectProperty> | <transitiveObjectProperty>

<subObjectPropertyOf>	::= "SubObjectPropertyOf(" <axiomAnnotations> " " <subObjectPropertyExpression> " " <superObjectPropertyExpression> ")" | "SubObjectPropertyOf(" <axiomAnnotations> " " <objectPropertyExpression> " " <simpleObjectPropertyExpression> ")" 
<subObjectPropertyExpression>	::= <objectPropertyExpression> | <propertyExpressionChain>
<propertyExpressionChain>	::= "ObjectPropertyChain(" <objectPropertyExpression> " " <objectPropertyExpression> " " <objectPropertyExpressionList> ")"
<superObjectPropertyExpression>	::= <nonSimpleObjectPropertyExpression>

<equivalentObjectProperties>	::= "EquivalentObjectProperties(" <axiomAnnotations> " " <objectPropertyExpression> " " <objectPropertyExpression> " " <objectPropertyExpressionList>")"

<objectPropertyDomain>	::= "ObjectPropertyDomain(" <axiomAnnotations> " " <objectPropertyExpression> " " <classExpression> ")"

<objectPropertyRange>	::= "ObjectPropertyRange(" <axiomAnnotations> " " <objectPropertyExpression> " " <classExpression> ")"

<reflexiveObjectProperty>	::= "ReflexiveObjectProperty(" <axiomAnnotations> " " <objectPropertyExpression> ")"

<transitiveObjectProperty>	::= "TransitiveObjectProperty(" <axiomAnnotations> " " <nonSimpleObjectPropertyExpression> ")"


<dataPropertyAxiom>	::= <subDataPropertyOf> | <equivalentDataProperties> | <dataPropertyDomain> | <dataPropertyRange> | <functionalDataProperty>


<subDataPropertyOf>	::= "SubDataPropertyOf(" <axiomAnnotations> " " <subDataPropertyExpression> " " <superDataPropertyExpression> ")"
<subDataPropertyExpression>	::= <dataPropertyExpression>
<superDataPropertyExpression>	::= <dataPropertyExpression>

<equivalentDataProperties>	::= "EquivalentDataProperties(" <axiomAnnotations> " " <dataPropertyExpression> " " <dataPropertyExpression> " " <dataPropertyExpressionList> ")"

<dataPropertyDomain>    ::= "DataPropertyDomain(" <axiomAnnotations> " " <dataPropertyExpression> " " <classExpression> ")"

<dataPropertyRange>	::= "DataPropertyRange(" <axiomAnnotations> " " <dataPropertyExpression> " " <dataRange> ")"

<functionalDataProperty>	::= "FunctionalDataProperty(" <axiomAnnotations> " " <dataPropertyExpression> ")"


#<dataTypeDefinition>	::= "DatatypeDefinition(" <axiomAnnotations> " " <NewDatatype> " " <dataRange> ")"


# also encodes "In each such axiom in an OWL ontology, m or n (or both) MUST be larger than zero" from https://www.w3.org/TR/2012/REC-owl2-syntax-20121211/#Keys

<hasKey>	::=  "HasKey(" <axiomAnnotations> " " <classExpression> " ( " <objectPropertyExpression> " " <objectPropertyExpressionList> " ) ( " <dataPropertyExpression> " " <dataPropertyExpressionList> " ) )" | "HasKey(" <axiomAnnotations> " " <classExpression> " ( " <objectPropertyExpression> " " <objectPropertyExpressionList> " ) ( ) )" | "HasKey(" <axiomAnnotations> " " <classExpression> " ( ) ( " <dataPropertyExpression> " " <dataPropertyExpressionList> " ) )"


<assertion>	::= <sameIndividual> | <differentIndividuals> | <classAssertion> | <objectPropertyAssertion> | <negativeObjectPropertyAssertion> | <dataPropertyAssertion> | <negativeDataPropertyAssertion>

<sourceIndividual>	::= <Individual>
<targetIndividual>	::= <Individual>
<targetValue>		::= <literal>

<sameIndividual>	::= "SameIndividual(" <axiomAnnotations> " " <Individual> " " <Individual> " " <individualList> ")" 

<differentIndividuals>	::= "DifferentIndividuals(" <axiomAnnotations> " " <Individual> " " <Individual> " " <individualList> ")" 

<classAssertion>	::= "ClassAssertion(" <axiomAnnotations> " " <classExpression> " " <Individual> ")" 

<objectPropertyAssertion>	::= "ObjectPropertyAssertion(" <axiomAnnotations> " " <objectPropertyExpression> " " <sourceIndividual> " " <targetIndividual> ")"

<negativeObjectPropertyAssertion>	::= "NegativeObjectPropertyAssertion(" <axiomAnnotations> " " <objectPropertyExpression> " " <sourceIndividual> " " <targetIndividual> ")"

<dataPropertyAssertion>	::= "DataPropertyAssertion(" <axiomAnnotations> " " <dataPropertyExpression> " " <sourceIndividual> " "  <targetValue> ")"

<negativeDataPropertyAssertion>	::= "NegativeDataPropertyAssertion(" <axiomAnnotations> " " <dataPropertyExpression> " " <sourceIndividual> " "  <targetValue> ")"





##################### terminals #########################


<Prefixes>      ::= "Prefix(:=<http://www.example.org/reasonerTester#>)\nPrefix(owl:=<http://www.w3.org/2002/07/owl#>)\nPrefix(rdf:=<http://www.w3.org/1999/02/22-rdf-syntax-ns#>)\nPrefix(xml:=<http://www.w3.org/XML/1998/namespace>)\nPrefix(xsd:=<http://www.w3.org/2001/XMLSchema#>)\nPrefix(rdfs:=<http://www.w3.org/2000/01/rdf-schema#>)\n\n"

<declarations> ::= <ClassDeclarations> <IndividualsDeclarations> <ObjectPropertiesDeclarations> <DataPropertiesDeclarations> <AnnotationPropertiesDeclaration> <DataTypeDeclarations> "\n"

<ClassDeclarations>		::= "Declaration(Class(:A))\nDeclaration(Class(:B))\nDeclaration(Class(:C))\nDeclaration(Class(:D))\n"

<IndividualsDeclarations>	::= "Declaration(NamedIndividual(:a))\nDeclaration(NamedIndividual(:b))\nDeclaration(NamedIndividual(:c))\nDeclaration(NamedIndividual(:d))\n"

#<ObjectPropertiesDeclarations>	::= "Declaration(ObjectProperty(:p))\nDeclaration(ObjectProperty(:q))\nDeclaration(ObjectProperty(:r))\nDeclaration(ObjectProperty(:s))\n"
<ObjectPropertiesDeclarations>	::= "Declaration(ObjectProperty(:psim))\nDeclaration(ObjectProperty(:qsim))\nDeclaration(ObjectProperty(:rsim))\nDeclaration(ObjectProperty(:ssim))\nDeclaration(ObjectProperty(:pnonsim))\nDeclaration(ObjectProperty(:qnonsim))\nDeclaration(ObjectProperty(:rnonsim))\nDeclaration(ObjectProperty(:snonsim))\n"

<DataPropertiesDeclarations>	::= "Declaration(DataProperty(:dp))\nDeclaration(DataProperty(:dq))\nDeclaration(DataProperty(:dr))\nDeclaration(DataProperty(:ds))\n"

<DataTypeDeclarations>		::= "Declaration(Datatype(:dta))\nDeclaration(Datatype(:dtb))\nDeclaration(Datatype(:dtc))\n"


<AnnotationPropertiesDeclaration> ::= "Declaration(AnnotationProperty(:anp))\nDeclaration(AnnotationProperty(:anq))\nDeclaration(AnnotationProperty(:anr))\nDeclaration(AnnotationProperty(:ans))\n"


<Class>         ::= ":A" | ":B" | ":C" | ":D"
<Individual>    ::= ":a" | ":b" | ":c" | ":d"
#<ObjectProperty>      ::= ":p" | ":q" | ":r" | ":s"
<SimpleObjectProperty>      ::= ":psim" | ":qsim" | ":rsim" | ":ssim"
<NonSimpleObjectProperty>      ::= ":pnonsim" | ":qnonsim" | ":rnonsim" | ":snonsim"


<DataProperty>	::= ":dp" | ":dq" | ":dr" | ":ds"
<Datatype>	::= "xsd:string" | "rdf:PlainLiteral" | "rdfs:Literal"
#<NewDatatype>	::= ":dta" | ":dtb" | ":dtc"	# introduced, to make "DataTypeDefinition" possible according to rules

#<LanguageTag>	::= "@en" | "@de" | "@fr" | "@no"
<QuotedString>	::=  "\"s1\"" | "\"s2\"" | "\"s3\"" | "\"s4\""

<AnnotationProperty>	::= ":anp" | ":anq" | ":anr" | ":ans"

<AnnotationValue>	::= ":annotation1" | ":annotation2" | ":annotation3" | ":annotation4"
